+++
title = "2a: Unit Logic"
weight = 4
template = "doc.html"
+++

### `++biff`

[`Unit`](/docs/hoon/reference/stdlib/1c#unit) as argument

Applies a function `b` that produces a [`unit`](/docs/hoon/reference/stdlib/1c#unit) to the unwrapped value of [`unit`](/docs/hoon/reference/stdlib/1c#unit)
`a` (`u.a`). If `a` is empty, `~` is produced.

#### Accepts

`a` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

`b` is a function that accepts a [`noun`](/docs/glossary/noun) and produces a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Produces

A [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Source

```hoon
    ++  biff
      |*  [a=(unit) b=$-(* (unit))]
      ?~  a  ~
      (b u.a)
```

#### Examples

```
    > (biff (some 5) |=(a=@ (some (add a 2))))
    [~ u=7]

    > (biff ~ |=(a=@ (some (add a 2))))
    ~
```


---
### `++bind`

Non-unit function to [`unit`](/docs/hoon/reference/stdlib/1c#unit), producing [`unit`](/docs/hoon/reference/stdlib/1c#unit)

Applies a function `b` to the value (`u.a`) of a [`unit`](/docs/hoon/reference/stdlib/1c#unit) `a`, producing
a [`unit`](/docs/hoon/reference/stdlib/1c#unit). Used when you want a function that does not accept or produce a
[`unit`](/docs/hoon/reference/stdlib/1c#unit) to both accept and produce a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Accepts

`a` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

`b` is a function.

#### Produces

A [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Source

```hoon
    ++  bind
      |*  [a=(unit) b=gate]
      ?~  a  ~
      [~ u=(b u.a)]
```


#### Examples

```
    > (bind ((unit @) [~ 97]) ,@t)
    [~ u='a']

    > =a |=(a=@ (add a 1))
    > (bind ((unit @) [~ 2]) a)
    [~ u=3]
```


---
### `++bond`

Replace null

Replaces an empty [`unit`](/docs/hoon/reference/stdlib/1c#unit) `b` with the product of a called [`trap`](/docs/glossary/trap)
`a`. If the [`unit`](/docs/hoon/reference/stdlib/1c#unit) is not empty, then the original [`unit`](/docs/hoon/reference/stdlib/1c#unit) is produced.

#### Accepts

`a` is a [`trap`](/docs/glossary/trap).

`b` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Produces

Either the product of `a` or the value inside of [`unit`](/docs/hoon/reference/stdlib/1c#unit) `b`.

#### Source

```hoon
    ++  bond
      |*  a=(trap)
      |*  b=(unit)
      ?~  b  $:a
      u.b
```

#### Examples

```
    > (bex 10)
    1.024
    > ((bond |.((bex 10))) ~)
    1.024

    > ((bond |.((bex 10))) (slaw %ud '123'))
    123
```


---
### `++both`

Group [`unit`](/docs/hoon/reference/stdlib/1c#unit) values into pair

Produces `~` if either `a` or `b` are empty. Otherwise, produces a
[`unit`](/docs/hoon/reference/stdlib/1c#unit) whose value is a cell of the values of two input [`units`](/docs/hoon/reference/stdlib/1c#unit) `a` and
`b`.

#### Accepts

`a` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

`b` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Produces

A [`unit`](/docs/hoon/reference/stdlib/1c#unit) of the two initial values.

#### Source

```hoon
    ++  both
      |*  [a=(unit) b=(unit)]
      ?~  a  ~
      ?~  b  ~
      [~ u=[u.a u.b]]
```

#### Examples

```
    > (both (some 1) (some %b))
    [~ u=[1 %b]]

    > (both ~ (some %b))
    ~
```

---
### `++clap`

Apply function to two [`units`](/docs/hoon/reference/stdlib/1c#unit)

Applies a binary function `c`--which does not usually accept or produce a
[`unit`](/docs/hoon/reference/stdlib/1c#unit)-- to the values of two [`units`](/docs/hoon/reference/stdlib/1c#unit), `a` and `b`, producing a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Accepts

`a` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

`b` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

`c` is a function that performs a binary operation.

#### Produces

A [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Source

```hoon
    ++  clap                                                ::  combine
      |*  [a=(unit) b=(unit) c=_|=(^ +<-)]
      ?~  a  b
      ?~  b  a
      [~ u=(c u.a u.b)]
```

#### Examples

```
    > =u ((unit @t) [~ 'a'])
    > =v ((unit @t) [~ 'b'])
    > (clap u v |=([a=@t b=@t] (welp (trip a) (trip b))))
    [~ u="ab"]

    > =a ((unit @u) [~ 1])
    > =b ((unit @u) [~ 2])
    > =c |=([a=@ b=@] (add a b))

    > (clap a b c)
    [~ 3]
```


---
### `++drop`

[`Unit`](/docs/hoon/reference/stdlib/1c#unit) to list

Makes a [`++list`](/docs/hoon/reference/stdlib/1c#list) of the unwrapped value (`u.a`) of a [`unit`](/docs/hoon/reference/stdlib/1c#unit) `a`.

#### Accepts

`a` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Produces

A list.

#### Source

```hoon
    ++  drop                                                ::  enlist
      |*  a=(unit)
      ?~  a  ~
      [i=u.a t=~]
```

#### Examples

```
    > =a ((unit @) [~ 97])
    > (drop a)
    [i=97 t=~]

    > =a ((unit @) [~])
    > (drop a)
    ~
```


---
### `++fall`

Give [`unit`](/docs/hoon/reference/stdlib/1c#unit) a default value

Produces a default value `b` for a [`unit`](/docs/hoon/reference/stdlib/1c#unit) `a` in cases where `a` is null.

#### Accepts

`a` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

`b` is a [`noun`](/docs/glossary/noun) that's used as the default value.

#### Produces

Either a [`noun`](/docs/glossary/noun) `b` or the unwrapped value of [`unit`](/docs/hoon/reference/stdlib/1c#unit) `a`.

#### Source

```hoon
    ++  fall                                                ::  default
      |*  [a=(unit) b=*]
      ?~(a b u.a)
```

#### Examples

```
    > (fall ~ 'a')
    'a'
    > (fall [~ u=0] 'a')
    0
```

---
### `++lift`

Curried bind

Accepts a `++gate` `a` and produces a function that accepts [`unit`](/docs/hoon/reference/stdlib/1c#unit)
`b` to which it applies `a`. Used when you want a function that does not accept
or produce a [`unit`](/docs/hoon/reference/stdlib/1c#unit) to both accept and produce a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Accepts

`a` is a gate.

`b` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Produces

A [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Source

```hoon
    ++  lift                                                ::  lift gate (fmap)
      |*  a=gate                                            ::  flipped
      |*  b=(unit)                                          ::  curried
      (bind b a)                                            ::  bind
```

#### Examples

```
    > ((lift dec) `(unit @)`~)
    ~

    > ((lift dec) `(unit @)`[~ 20])
    [~ 19]
```


---
### `++mate`

Choose

Accepts two [`units`](/docs/hoon/reference/stdlib/1c#unit) `a` and `b` whose values are expected to be
equivalent. If either is empty, then the value of the other is produced.
If neither are empty, it asserts that both values are the same and
produces that value. If the assertion fails, `++mate` crashes with
`'mate'` in the stack trace.

#### Accepts

`a` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

`b` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Produces

A [`unit`](/docs/hoon/reference/stdlib/1c#unit) or crash.

#### Source

```hoon
    ++  mate                                                ::  choose
      |*  [a=(unit) b=(unit)]
      ?~  b  a
      ?~  a  b
      ?.(=(u.a u.b) ~|('mate' !!) a)
```

#### Examples

```
    > =a ((unit @) [~ 97])
    > =b ((unit @) [~ 97])
    > (mate a b)
    [~ 97]

    > =a ((unit @) [~ 97])
    > =b ((unit @) [~])
    > (mate a b)
    [~ 97]

    > =a ((unit @) [~ 97])
    > =b ((unit @) [~ 98])
    > (mate a b)
    ! 'mate'
    ! exit
```


---
### `++need`

Unwrap [`unit`](/docs/hoon/reference/stdlib/1c#unit)

Retrieve the value from a [`unit`](/docs/hoon/reference/stdlib/1c#unit) and crash if the [`unit`](/docs/hoon/reference/stdlib/1c#unit) is null.

#### Accepts

`a` is a [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Produces

Either the unwrapped value of `a` (`u.a`), or crash.

#### Source

```hoon
    ++  need                                                ::  demand
      |*  a=(unit)
      ?~  a  ~|(%need !!)
      u.a
```

#### Examples

```
    > =a ((unit [@t @t]) [~ ['a' 'b']])
    > (need a)
    ['a' 'b']

    > =a ((unit @ud) [~ 17])
    > (need a)
    17

    > =a ((unit @) [~])
    > (need a)
    ! exit
```


---
### `++some`

Wrap value in a [`unit`](/docs/hoon/reference/stdlib/1c#unit)

Takes any [`noun`](/docs/glossary/noun) `a` and produces a [`unit`](/docs/hoon/reference/stdlib/1c#unit) with the value set to `a`.

#### Accepts

`a` is a [`noun`](/docs/glossary/noun).

#### Produces

A [`unit`](/docs/hoon/reference/stdlib/1c#unit).

#### Source

```hoon
    ++  some                                                ::  lift (pure)
      |*  a=*
      [~ u=a]
```

#### Examples

```
    > (some ['a' 'b'])
    [~ u=['a' 'b']]

    > (some &)
    [~ u=%.y]
```


---
